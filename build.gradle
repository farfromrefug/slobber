plugins {
    id 'java-library'
}

def getVersionName = {
    return "git describe --tags".execute().text.trim()
}

version = getVersionName()

sourceCompatibility = '1.7'

repositories {
    mavenCentral()
}

jar {

    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    manifest {
        attributes(
        "Main-Class": "itkach.slobber.Slobber",
        "Implementation-Title": "Slobber",
        "Implementation-Version": version)
    }
}


task fatJar(type: Jar) {
    manifest.from jar.manifest
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    archiveBaseName = project.name + '-all'
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    project("slobj").file("exclude.txt").eachLine {
        line ->
            println line
            if (!line.startsWith("#")) {
                exclude line.trim()
            }
    }
    with jar
}


dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.4.2'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.4.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.4.2'
    api files('libs/simple-5.1.7.1.itkach.jar')
    api project(':slobj')
 }
